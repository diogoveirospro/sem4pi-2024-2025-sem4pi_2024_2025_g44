# US251 - Specification of the language for figure and show description

**As** a PO,
**I want** a high-level neutral language,
**So that** I can describe figures to be specified.

Grammar Show
```
showDefinition   --> "show" ID? ";" figureList;

figureList       --> figureRef { "," figureRef } ";";

figureRef        --> ID;

ID               --> [a-zA-Z][a-zA-Z0-9]*;
```

Grammar Figure 

```

DSL            --> "version" VERSIONNUMBER ";" declarations statements;

VERSIONNUMBER  --> DIGIT "." DIGIT "." TWO_DIGITS;

declarations   --> { declaration };

declaration    --> "DroneType" IDENTIFIER ";"
                | "Position" IDENTIFIER "=" vector ";"
                | "Velocity" IDENTIFIER "=" ( "PI" "/" DOUBLENUMBER | DOUBLENUMBER ) ";"
                | "Distance" IDENTIFIER "=" DOUBLENUMBER ";"
                | shapeDecl;

shapeDecl      --> shapeType IDENTIFIER "(" argumentList ")" ";";
shapeType      --> "Line" | "Rectangle" | "Circle" | "Circumference";

argumentList   --> expression { "," expression };
statements     --> { statement };

statement      --> section | action ";";

section        --> "before" { statement } "endbefore"
                | "after" { statement } "endafter"
                | "group" { statement } "endgroup";

action         --> IDENTIFIER "." actionType "(" argumentList? ")"
                | IDENTIFIER "." "lightsOff"
                | "pause" "(" expression ")";

actionType     --> "move" | "movePos" | "rotate" | "lightsOn";

expression     --> term { ("+" | "-") term };
term           --> factor { ("*" | "/") factor };
factor         --> DOUBLENUMBER
                | "PI"
                | IDENTIFIER
                | vector
                | "(" expression ")";

vector         --> "(" DOUBLENUMBER "," DOUBLENUMBER "," DOUBLENUMBER ")";

TWO_DIGITS     --> DIGIT DIGIT;
DIGIT          --> [0-9];
DOUBLENUMBER   --> DIGIT+ ("." DIGIT+)?;

IDENTIFIER     --> [a-zA-Z][a-zA-Z0-9]*;

```